#!/usr/bin/env coffee
# -*- mode: coffee; -*-

read = require "read"
parser = require "./lib/parser"

{ Application } = require "apiaxle.base"

# create a new application for scripting
class AxleRepl extends Application

# command loading
{ Api } = require "./command/api"

all_commands =
  api: Api

axle = new AxleRepl()
axle.script ( cb ) ->
  topLevelInput()
  cb()

topLevelInput = ( ) ->
  read { prompt: "axle> " }, ( err, entry ) ->
    throw err if err

    entered_commands = parser entry

    # get the initial highlevel command
    command = entered_commands.shift()

    # quit/exit are slightly magic
    return if command in [ "quit", "exit" ]

    if not all_commands[ command ]?
      console.error "I don't know about '#{ command }'. Try 'help' instead."
      return topLevelInput()

    # init the class
    command_object = new all_commands[ command ]( axle )

    # the command exists, is there a method for it though?
    method = ( entered_commands.shift() or "help" )
    if not command_object[ method ]?
      console.error "'#{ command }' doesn't have a '#{ method }' method."
      return topLevelInput()

    # run the method
    command_object[ method ]( entered_commands, topLevelInput )
